### ‚ú® Minte as NFTs

Agora que temos todos os dados configurados para nossos personagens, a pr√≥xima coisa a fazer √© mintar a NFT. Vamos passar pelo processo. Aqui est√° meu contrato atualizado, e eu comentei as linhas que mudei ou adicionei coisas para ficar mais f√°cil!

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

// Contrato NFT para herdar.
import "@openzeppelin/contracts/token/ERC721/ERC721.sol";

// Fun√ß√µes de ajuda que o OpenZeppelin providencia.
import "@openzeppelin/contracts/utils/Counters.sol";
import "@openzeppelin/contracts/utils/Strings.sol";


import "hardhat/console.sol";

// Nosso contrato herda do ERC721, que √© o contrato padr√£o de
// NFT!
contract MyEpicGame is ERC721 {

  struct CharacterAttributes {
    uint characterIndex;
    string name;
    string imageURI;
    uint hp;
    uint maxHp;
    uint attackDamage;
  }

  // O tokenId √© o identificador √∫nico das NFTs, √© um n√∫mero
  // que vai incrementando, como 0, 1, 2, 3, etc.

  using Counters for Counters.Counter;
  Counters.Counter private _tokenIds;

  CharacterAttributes[] defaultCharacters;

  // N√≥s criamos um mapping do tokenId => atributos das NFTs.
  mapping(uint256 => CharacterAttributes) public nftHolderAttributes;

  // Um mapping de um endere√ßo => tokenId das NFTs, nos d√° um
  // jeito f√°cil de armazenar o dono da NFT e referenciar ele
  // depois.
  mapping(address => uint256) public nftHolders;

  constructor(
    string[] memory characterNames,
    string[] memory characterImageURIs,
    uint[] memory characterHp,
    uint[] memory characterAttackDmg
    // Embaixo, voc√™ tamb√©m pode ver que adicionei um s√≠mbolo especial para identificar nossas NFTs
    // Esse √© o nome e o s√≠mbolo do nosso token, ex Ethereum ETH.
    // Eu chamei o meu de Heroes e HERO. Lembre-se, um NFT √© s√≥ um token!
  )
    ERC721("Heroes", "HERO")
  {
    for(uint i = 0; i < characterNames.length; i += 1) {
      defaultCharacters.push(CharacterAttributes({
        characterIndex: i,
        name: characterNames[i],
        imageURI: characterImageURIs[i],
        hp: characterHp[i],
        maxHp: characterHp[i],
        attackDamage: characterAttackDmg[i]
      }));

      CharacterAttributes memory c = defaultCharacters[i];

      // O uso do console.log() do hardhat nos permite 4 par√¢metros em qualquer order dos seguintes tipos: uint, string, bool, address

      console.log("Done initializing %s w/ HP %s, img %s", c.name, c.hp, c.imageURI);
    }

    // Eu incrementei tokenIds aqui para que minha primeira NFT tenha o ID 1.
    // Mais nisso na aula!
    _tokenIds.increment();
  }

  // Usu√°rios v√£o poder usar essa fun√ß√£o e pegar a NFT baseado no personagem que mandarem!
  function mintCharacterNFT(uint _characterIndex) external {
    // Pega o tokenId atual (come√ßa em 1 j√° que incrementamos no constructor).
    uint256 newItemId = _tokenIds.current();

    // A fun√ß√£o m√°gica! Atribui o tokenID para o endere√ßo da carteira de quem chamou o contrato.

    _safeMint(msg.sender, newItemId);

    // N√≥s mapeamos o tokenId => os atributos dos personagens. Mais disso abaixo

    nftHolderAttributes[newItemId] = CharacterAttributes({
      characterIndex: _characterIndex,
      name: defaultCharacters[_characterIndex].name,
      imageURI: defaultCharacters[_characterIndex].imageURI,
      hp: defaultCharacters[_characterIndex].hp,
      maxHp: defaultCharacters[_characterIndex].maxHp,
      attackDamage: defaultCharacters[_characterIndex].attackDamage
    });

    console.log("Minted NFT w/ tokenId %s and characterIndex %s", newItemId, _characterIndex);

    // Mant√©m um jeito f√°cil de ver quem possui a NFT
    nftHolders[msg.sender] = newItemId;

    // Incrementa o tokenId para a pr√≥xima pessoa que usar.
    _tokenIds.increment();
  }
}
```

Muitas coisas acontecendo aqui. A primeira coisa que fiz foi criar duas vari√°veis de estado que s√£o quase como vari√°veis globais permanentes no contrato. Leia sobre elas [aqui](https://ethereum.stackexchange.com/a/25556).

```javascript
mapping(uint256 => CharacterAttributes) public nftHolderAttributes;
mapping(address => uint256) public nftHolders;
```

`nftHolderAttributes` vai ser onde n√≥s armazenamos o estado das NFTs dos jogadores. N√≥s mapeamos o id da NFT para uma estrutura `CharacterAttributes`.

Lembre-se, cada jogador tem o pr√≥prio personagem NFT. E, cada NFT tem o pr√≥prio estado como `HP`, `Dano de Ataque`, etc! Ent√£o se o player #172 tem uma NFT "Pikachu" e o Pikachu dele perder vida em uma batalha, **ent√£o s√≥ a NFT Pikachu do jogador #172 deve ser mudada**, e a NFT Pikachu de todo o resto das pessoas deve se manter a mesma! Ent√£o, n√≥s armazenamos o n√≠vel dos dados do personagem desse jogador em um map.

Depois, eu tenho `nftHolders` que basicamente me deixa mapear facilmente o endere√ßo de um usu√°rio para o ID da NFT que eles possuem. Por exemplo, eu poderia fazer `nftHolders[INSIRA_UM_ENDERECO_AQUI]` e instantaneamente saber qual NFT que aquele endere√ßo possui. Ajuda muito manter esses dados no contrato para que seja facilmente acess√≠vel.

### ‚ö°Ô∏è ERC 721

Voc√™ tamb√©m vai ver que eu "herdo" um contrato OpenZeppelin usando `is ERC721`  quando eu declaro o contrato. Voc√™ pode ler mais sobre hereditariedade [aqui](https://solidity-by-example.org/inheritance/), mas basicamente, significa que podemos chamar outros contratos a partir do nosso. √â quase como importar fun√ß√µes para usarmos.

O padr√£o NFT √© conhecido como `ERC721` , o qual voc√™ pode ler um pouco mais sobre [aqui](https://eips.ethereum.org/EIPS/eip-721). OpenZeppelin essencialmente implementa o padr√£o NFT para n√≥s e nos deixa escrever nossa pr√≥pria l√≥gica em cima disso para customiz√°-lo. Isso significa que n√£o precisamos escrever c√≥digo repetitivo e b√°sico.

Seria loucura escrever um servidor HTTP do zero sem usar uma biblioteca, certo? Claro, a menos que voc√™ quisesse explorar. De maneira semelhante - seria loucura apenas escrever um contrato NFT do zero! Voc√™ pode explorar o contrato `ERC721`  que estamos herdando  [daqui](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC721/ERC721.sol).

```solidity
_tokenIds.increment();
```

Ent√£o, `_tokenIds` come√ßa no `0`. √â s√≥ um contador. `increment()¬¥ s√≥ adiciona mais 1 - veja [aqui](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/fa64a1ced0b70ab89073d5d0b6e01b0778f7e7d6/contracts/utils/Counters.sol#L32).

**No constructor** eu incremento ele em 1. Por qu√™? Basicamente porque eu n√£o gosto de lidar com zeros no meu c√≥digo. Em Solidity, 0 √© um [valor padr√£o](https://docs.soliditylang.org/en/v0.5.11/control-structures.html#scoping-and-declarations) e eu tento me manter longe de valores padr√£o. S√≥ confie em mim por agora ;).

Eu tamb√©m tenho `increment()` em `mintCharacterNFT` mas n√£o esque√ßa de adicion√°-la no `constructor` tamb√©m ;).

```solidity
function mintCharacterNFT(uint _characterIndex)
```

Essa fun√ß√£o √© onde o minting est√° acontecendo.

Primeiro, voc√™ vai ver que passamos `_characterIndex` para a fun√ß√£o. Por qu√™?

Bom, porque os jogadores precisam estar aptos a falar para n√≥s **Qual personagem eles querem**! Por exemplo, se eu fizer `mintCharacterNFT(0)` ent√£o o personagem com as estat√≠sticas de `defaultCharacters[0]` ser√° mintado!

```solidity
uint256 newItemId = _tokenIds.current();
```

Daqui n√≥s temos um n√∫mero chamado `newItemId`. Esse √© o id da NFT. Lembre-se, cada NFT √© um ID √∫nico. √â s√≥ um contador b√°sico. `_tokenIds.current()` come√ßa no 0, mas, n√≥s fizemos `_tokenIds.increment()` no `constructor` para que esteja no 1.

N√≥s estamos usando `_tokenIds` para manter a contagem dos identificadores √∫nicos das NFTs, e √© s√≥ um n√∫mero! Ent√£o, quando chamamos primeiro `mintCharacterNFT`, `newItemId` √© 1. Quando rodamos de novo, `newItemId` ser√° 2 e assim por diante.

`_tokenIds` √© uma **vari√°vel de estado** o que significa que se mudarmos, o valor √© armazenado diretamente no contrato como uma vari√°vel global que fica permanentemente na mem√≥ria.

```solidity
_safeMint(msg.sender, newItemId).
```

Essa √© a linha m√°gica! Quando fazemos `_safeMint(msg.sender, newItemId)`  est√° basicamente dizendo: "minte a NFT com o id `newItemId` para o usu√°rio com endere√ßo `msg.sender`". Aqui, `msg.sender` √© uma vari√°vel que o  [Solidity providencia](https://docs.soliditylang.org/en/develop/units-and-global-variables.html#block-and-transaction-properties) que nos d√° f√°cil acesso ao  **endere√ßo p√∫blico** da pessoa que estiver chamando o contrato.

**Voc√™ n√£o pode chamar um contrato anonimamente**, voc√™ precisa ter as credenciais da sua carteira conectadas. Isso √© quase como "fazendo login" e ser autenticado :).

O que √© incr√≠vel aqui √© que essa √© uma  **maneira super segura de conseguir o endere√ßo p√∫blico do usu√°rio**. Manter o endere√ßo p√∫blico em segredo n√£o √© um problema, j√° √© p√∫blico, todo mundo consegue enxergar. Mas, usando `msg.sender` voc√™ n√£o consegue fingir ser o endere√ßo p√∫blico de outra pessoa a n√£o ser que voc√™ tenha as credenciais da carteira dela!

### üé® Mantendo dados din√¢micos em uma NFT.

Ent√£o, na medida que jogadores jogam o jogo, certos valores em seus personagens v√£o mudar, certo? Por exemplo, se meu personagem atacar o boss, o boss vai atacar de volta! **Nesse caso, o HP da minha NFT vai precisar ser diminu√≠do.** N√≥s precisamos de uma maneira de armazenar esses dados por jogador:

```solidity
nftHolderAttributes[newItemId] = CharacterAttributes({
  characterIndex: _characterIndex,
  name: defaultCharacters[_characterIndex].name,
  imageURI: defaultCharacters[_characterIndex].imageURI,
  hp: defaultCharacters[_characterIndex].hp,
  maxHp:defaultCharacters[_characterIndex].maxHp,
  attackDamage: defaultCharacters[_characterIndex].attackDamage
});
```

Muitas coisas acontecendo aqui! Basicamente, **nossa NFT segura dados relacionados √† NFT do nosso jogador. Mas, esses dados s√£o din√¢micos. Por exemplo**, digamos que eu criasse uma NFT. Por padr√£o minha NFT come√ßa com estat√≠sticas padr√µes como:

```json
{
  "characterIndex": 1,
  "name": "Aang",
  "imageURI": "https://i.imgur.com/xVu4vFL.png",
  "hp": 200,
  "maxHp": 200,
  "attackDamage": 50
}
```

**Lembre-se, todo jogador tem seu pr√≥prio personagem NFT e a NFT mesma segura os dados do estado do personagem.**

Digamos que o meu personagem seja atacado e perca 50 de HP, bom, ent√£o o HP iria de 200 -> 150, certo? Esse valor vai precisar mudar na NFT!

```json
{
  "characterIndex": 1,
  "name": "Aang",
  "imageURI": "https://i.imgur.com/xVu4vFL.png",
  "hp": 150,
  "maxHp": 200,
  "attackDamage": 50
}
```

Ou talvez n√≥s queremos que nosso jogo tenham personagens **atualiz√°veis, que possam subir de n√≠vel e ter itens**, onde voc√™ poderia dar ao seu personagem uma espada que adicionaria +10 de dano de ataque, de 50 -> 60. Ent√£o, `attackDamage` precisaria mudar na NFT!

As pessoas muitas vezes pensam que os metadados n√£o permitem mudan√ßas, mas isso n√£o √© verdade. √â o criador quem decide!!!

Nesse caso, o nome e a imagem do personagem **nunca** mudam, mas o valor do HP definitivamente muda! **Nossas NFTs** precisam estar aptas a manter o estado do personagem espec√≠fico do jogador.

Esse √© o porqu√™ de n√≥s precisarmos da vari√°vel `nftHolderAttributes`que mapeia o tokenId da NFT para uma estrutura de `CharacterAttributes`. Nos permite atualizar facilmente os valores das NFTs do jogador. Isso significa quando o jogador joga nosso jogo e o `hp` da NFT dele muda (pois o boss ataca ele), n√≥s na verdade mudamos o valor do `hp` em `nftHolderAttributes`. E assim √© como n√≥s armazenamos os dados espec√≠ficos da NFT de cada jogador no nosso contrato!

Depois, fazemos:

```solidity
nftHolders[msg.sender] = newItemId;
```

Mapeia o endere√ßo p√∫blico da carteira para o tokenId das NFTs. Isso √© o que nos deixa manter a contagem de quem possui as NFTs facilmente.

_Nota: Nesse momento isso √© desenhado de maneira que cada jogador possa ter apenas um personagem NFT por endere√ßo de carteira. Se voc√™ quisesse, voc√™ poderia ajustar isso para os jogadores poderem ter m√∫ltiplos personagens, mas eu fiquei com 1 por jogador para facilitar! Esse √© nosso jogo, fa√ßa o que quiser!_

```solidity
_tokenIds.increment();
```

Depois que a NFT √© mintada, n√≥s incrementamos `tokenIds` usando `_tokenIds.increment()` (que √© uma fun√ß√£o que o OpenZeppelin nos d√°). Isso nos d√° a certeza de que da pr√≥xima vez que uma NFT for mintada, vai ter um identificador `tokenIds` diferente. Ningu√©m pode ter um `tokenIds` que j√° foi mintado.

### üò≥ Rodando localmente.

Em `run.js` o que precisamos fazer √© chamar `mintCharacterNFT`. Eu adicionei as linhas seguintes em `run.js` logo embaixo de onde escrevemos o endere√ßo do contrato.

```javascript
let txn;
// S√≥ temos tr√™s personagens.
// Uma NFT com personagem no index 2 da nossa array.
txn = await gameContract.mintCharacterNFT(2);
await txn.wait();

// Pega o valor da URI da NFT
let returnedTokenUri = await gameContract.tokenURI(1);
console.log("Token URI:", returnedTokenUri);
```

Quando fazemos `mintCharacterNFT(2)` o Hardhat vai chamar essa fun√ß√£o com uma **carteira padr√£o** que est√° configurada para n√≥s localmente. Isso significa que `msg.sender` ser√° o endere√ßo p√∫blico da nossa carteira local! **Isso √© outro motivo do porque o Hardhat √© t√£o legal,** ele nos deixa usar carteiras locais facilmente!! Isso √© geralmente uma dor enorme para fazer voc√™ mesmo.

A fun√ß√£o `tokenURI` √© algo que pegamos de gra√ßa do `ERC721` j√° que herdamos dele.

Basicamente, `tokenUri` √© uma fun√ß√£o em **cada NFT** que retorna os **dados atuais** que est√£o ligados √† NFT. Ent√£o quando eu chamo `gameContract.tokenURI(1)` est√° basicamente dizendo, _"v√° pegar para mims os dados dentro da NFT com tokenId 1"_, que seria a primeira NFT mintada. E, deveria me devolver todas as coisas, como o nome do personagem, o hp atual e etc.

Plataformas como o OpenSea e Rarible sabem como pegar o `tokenURI` j√° que a forma padr√£o de pegar os metadados da NFT. Vamos tentar rodar o nosso contrato de novo (lembre-se que o comando √© `npx hardhat run scripts/run.js`)

Meu output (sa√≠da) se parece com isso:

```plaintext
Done initializing Leo w/ HP 100, img https://i.imgur.com/pKd5Sdk.png
Done initializing Aang w/ HP 200, img https://i.imgur.com/xVu4vFL.png
Done initializing Pikachu w/ HP 300, img https://i.imgur.com/u7T87A6.png
Contrato implantado no endere√ßo: 0x5FbDB2315678afecb367f032d93F642f64180aa3
Mintou um NFT com tokenId 1 e characterIndex 2
Token URI:
```

**Hmmmmmm**. Token URI n√£o escreve nada! Isso significa que n√£o temos nenhum dado ligado √† nossa NFT. Mas espera, isso n√£o faz sentido. N√≥s n√£o configuramos os dados com `nftHolderAttributes`?

**N√£o. `nftHolderAttributes` n√£o ligou os dados √†s NFTs de nenhuma maneira. √â s√≥ um mapping que vive no contrato nesse momento.** O que vamos fazer agora √© basicamente fixar `nftHolderAttributes` para o `tokenURI` sobrescrevendo ele :).

### ‚≠êÔ∏è Configurando tokenURI

O `tokenURI` tem um formato espec√≠fico, na verdade! Na verdade, est√° esperando os dados NFT em **JSON**.

Vamos ver como fazer isso :).

Crie uma pasta nova dentro de `contracts` chamada `libraries`. Crie um arquivo chamado `Base64.sol` e coloque ele dentro de libraries. Copie e cole o c√≥digo [REVIEW(Eu tenh o o link do seu gist mas eu vi uma diferen√ßa na vers√£o do solidity, o farza ta com a 0.8.0 e vc ta usando a 0.8.1, n√£o sei se tem mais diferen√ßas mas queria que vc olhasse por favor.)](https://gist.github.com/farzaa/f13f5d9bda13af68cc96b54851345832) dentro de `Base64.sol`. Isso basicamente nos d√° fun√ß√µes que nos ajudam a codificar qualquer tipo de dado em uma string Base64 - que √© um padr√£o para codificar peda√ßos de dado em uma string. N√£o se preocupe, voc√™ vai ver como isso funciona logo!

Vamos precisar importar essa biblioteca no nosso contrato. Para isso, adicione o snippet a seguir perto do topo do seu arquivo, junto com os outros imports.

```solidity
// Helper que escrevemos para codificar em Base64
import "./libraries/Base64.sol";
```

Depois, escrevemos uma fun√ß√£o chamada `tokenURI` em `MyEpicGame.sol`.

```solidity
function tokenURI(uint256 _tokenId) public view override returns (string memory) {
  CharacterAttributes memory charAttributes = nftHolderAttributes[_tokenId];

  string memory strHp = Strings.toString(charAttributes.hp);
  string memory strMaxHp = Strings.toString(charAttributes.maxHp);
  string memory strAttackDamage = Strings.toString(charAttributes.attackDamage);

  string memory json = Base64.encode(
    abi.encodePacked(
      '{"name": "',
      charAttributes.name,
      ' -- NFT #: ',
      Strings.toString(_tokenId),
      '", "description": "Esta NFT d√° acesso ao meu jogo NFT!", "image": "',
      charAttributes.imageURI,
      '", "attributes": [ { "trait_type": "Health Points", "value": ',strHp,', "max_value":',strMaxHp,'}, { "trait_type": "Attack Damage", "value": ',
      strAttackDamage,'} ]}'
    )
  );

  string memory output = string(
    abi.encodePacked("data:application/json;base64,", json)
  );

  return output;
}
```

Isso parece complexo. Mas, n√£o √© t√£o louco! Primeiro n√≥s come√ßamos aqui:

```solidity
CharacterAttributes memory charAttributes = nftHolderAttributes[_tokenId];
```

Essa linha **recupera** os dados dessa NFT espec√≠fica buscando por ele usando `_tokenId` que foi passado para a fun√ß√£o. Ent√£o, se eu tivesse feito `tokenURI(256)` ele retornaria o JSON com os dados relacionados a NFT 256 (se existisse!).

Ent√£o, pegamos todos esses dados e empacotamos ele em uma vari√°vel chamada `json`. A estrutura JSON se parece com isso (quando est√° limpa):

```json
{
  "name": "Aang",
  "description": "Aqui √© a descri√ß√£o",
  "image": "https://i.imgur.com/xVu4vFL.png",
  "attributes": [
    { "trait_type": "Health Points", "value": 200, "max_value": 200 },
    { "trait_type": "Attack Damage", "value": 50 }
  ]
}
```

Voc√™ poder ler mais sobre a estrutura dos dados [aqui](https://docs.opensea.io/docs/metadata-standards#metadata-structure).

Ent√£o, isso pode parecer loucura, mas s√≥ est√° estruturando os dados para seguir o formato a seguir:

```solidity
string memory json = Base64.encode(
  abi.encodePacked(
        '{"name": "',
        charAttributes.name,
        ' -- NFT #: ',
        Strings.toString(_tokenId),
        '", "description": "Esta NFT d√° acesso ao meu jogo NFT!", "image": "',
        charAttributes.imageURI,
        '", "attributes": [ { "trait_type": "Health Points", "value": ',strHp,', "max_value":',strMaxHp,'}, { "trait_type": "Attack Damage", "value": ',
        strAttackDamage,'} ]}'
  )
);
```

N√≥s configuramos coisas como o nome da NFT, o HP, o Dano de ataque e etc **din√¢micamente**. _Nota: abi.encodePacked s√≥ combina strings._ Isso √© realmente legal porque n√≥s podemos mudar coisas como o HP da NFT e a imagem dela se quis√©ssemos, e atualizar na NFT! **√â din√¢mico!**

Tamb√©m esse padr√£o de metadados √© seguido por muitos sites populares de NFT como o OpenSea. Ent√£o, tudo que estamos fazendo na fun√ß√£o √© formatando a nossa vari√°vel `json` para seguir os padr√µes. Note: `max_value` n√£o √© necess√°rio, mas eu quis adicion√°-lo por divers√£o.

```solidity
abi.encodePacked("data:application/json;base64,", json)
```

Essa linha √© na verdade dif√≠cil de explicar, √© mais f√°cil apenas mostrar! V√° em frente e rode `run.js`. Aqui est√° meu output:

```plaintext
Done initializing Leo w/ HP 100, img https://i.imgur.com/pKd5Sdk.png
Done initializing Aang w/ HP 200, img https://i.imgur.com/xVu4vFL.png
Done initializing Pikachu w/ HP 300, img https://i.imgur.com/u7T87A6.png
Contrato implantado no endere√ßo: 0x5FbDB2315678afecb367f032d93F642f64180aa3
Mintou um NFT com tokenId 1 e characterIndex 2
Token URI: data:application/json;base64,eyJuYW1lIjogIlBpa2FjaHUgLS0gTkZUICM6IDEiLCAiZGVzY3JpcHRpb24iOiAiQ3JpdGljYWxIaXQgaXMgYSB0dXJuLWJhc2VkIE5GVCBnYW1lIHdoZXJlIHlvdSB0YWtlIHR1cm5zIHRvIGF0dGFjayB0aGUgYm9vcy4iLCAiaW1hZ2UiOiAiaHR0cHM6Ly9pLmltZ3VyLmNvbS91N1Q4N0E2LnBuZyIsICJhdHRyaWJ1dGVzIjogWyB7ICJ0cmFpdF90eXBlIjogIkhlYWx0aCBQb2ludHMiLCAidmFsdWUiOiAzMDAsICJtYXhfdmFsdWUiOjMwMH0sIHsgInRyYWl0X3R5cGUiOiAiQXR0YWNrIERhbWFnZSIsICJ2YWx1ZSI6IDI1fSBdfQ==
```

Voc√™ ver√° que Token URI agora escreve coisas! **Boa!!** V√° em frente e copie essa grande string depois de `Token URI:`. Por exemplo, a minha se parece com isso:

```plaintext
data:application/json;base64,eyJuYW1lIjogIlBpa2FjaHUgLS0gTkZUICM6IDEiLCAiZGVzY3JpcHRpb24iOiAiQ3JpdGljYWxIaXQgaXMgYSB0dXJuLWJhc2VkIE5GVCBnYW1lIHdoZXJlIHlvdSB0YWtlIHR1cm5zIHRvIGF0dGFjayB0aGUgYm9vcy4iLCAiaW1hZ2UiOiAiaHR0cHM6Ly9pLmltZ3VyLmNvbS91N1Q4N0E2LnBuZyIsICJhdHRyaWJ1dGVzIjogWyB7ICJ0cmFpdF90eXBlIjogIkhlYWx0aCBQb2ludHMiLCAidmFsdWUiOiAzMDAsICJtYXhfdmFsdWUiOjMwMH0sIHsgInRyYWl0X3R5cGUiOiAiQXR0YWNrIERhbWFnZSIsICJ2YWx1ZSI6IDI1fSBdfQ==
```

Cole essa string dentro da barra de URL no seu browser. Voc√™ vai ver algo como isso:

![REVIEW](https://i.imgur.com/C3QmD2G.png)

BOOOM!!!

Basicamente o que fizemos foi que formatamos nosso arquivo JSON e **codificamos ele** em Base64. Ent√£o, acontece que o arquivo JSON se torna essa string codificada super longa, que √© leg√≠vel para o nosso browser quando usamos o prefixo `data:application/json;base64,`.

Adicionamos `data:application/json;base64,` porque o nosso browser precisa saber como ler a string codificada que estamos passando. Nesse caso, estamos dizendo,

"Ei, estamos passando um arquivo JSON codificado com Base64, por favor renderize ele de maneira adequada."

De novo, isso √© considerado um padr√£o para a maioria dos navegadores, o que √© perfeito porque n√≥s queremos que os dados das nossas NFT sejam compat√≠veis com o maior n√∫mero de sistemas poss√≠vel.

Por qu√™ estamos fazendo essas coisas de Base64? Bom, basicamente isso √© como sites populares ocomo o OpenSea, Rarible e muitos outros preferem quando passam dados JSON diretamente do nosso conrtato.

**Incr√≠vel**. Ent√£o, agora estamos no ponto em que mintamos oficialmente e localmente as NFTs e a NFT tem dados fixados nela em uma maneira que siga os padr√µes.

**Estamos prontos para fazer o deploy da nossa NFT no OpenSea :).**

### üö®¬†Reporte seu Progresso !

Poste uma screenshot do seu JSON quando voc√™ colou o `tokenURI` no endere√ßo do seu navegador :)!